apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "shot"

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.karumi.superheroes.jetpack"
    minSdkVersion 16
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "com.karumi.TestRunner"
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString(),
                     "room.incremental":"true",
                     "room.expandProjection":"true"]
      }
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }
  dataBinding {
    enabled = true
  }
}

configurations {
  ktlint
}

dependencies {
  /* Lint */
  ktlint "com.pinterest:ktlint:0.32.0"

  /* Kotlin */
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  /* Jetpack */
  implementation "androidx.appcompat:appcompat:1.1.0"
  implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta4"
  implementation "androidx.recyclerview:recyclerview:1.1.0"
  implementation "androidx.lifecycle:lifecycle-runtime:2.2.0"
  implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
  implementation "androidx.paging:paging-runtime-ktx:2.1.1"
  implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0"
  implementation "android.arch.navigation:navigation-ui-ktx:1.0.0"
  implementation "androidx.room:room-runtime:2.2.4"
  implementation "android.arch.work:work-runtime-ktx:1.0.1"
  kapt "androidx.room:room-compiler:2.2.4"
  implementation "androidx.fragment:fragment:1.2.2"
  debugImplementation "androidx.fragment:fragment-testing:1.2.2"

  /* DI */
  implementation "org.kodein.di:kodein-di-erased-jvm:6.0.1"
  implementation "org.kodein.di:kodein-di-framework-android-x:6.0.1"

  /* UI */
  implementation "com.squareup.picasso:picasso:2.71828"
  implementation "com.google.android.material:material:1.1.0"

  /* Testing */
  testImplementation "androidx.test.ext:junit:1.1.1"
  testImplementation "org.mockito:mockito-core:2.27.0"
  testImplementation "androidx.room:room-testing:2.2.4"
  androidTestImplementation "androidx.test.ext:junit:1.1.1"
  androidTestImplementation "org.mockito:mockito-android:2.27.0"
  androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
  androidTestImplementation "com.github.tmurakami:dexopener:2.0.2"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:3.2.0"
  androidTestImplementation "androidx.test.espresso:espresso-intents:3.2.0"
  androidTestImplementation "androidx.room:room-testing:2.2.4"
  androidTestImplementation "android.arch.work:work-testing:1.0.1"
}

shot {
  appId = "com.karumi.superheroes.jetpack"
}

task ktlint(type: JavaExec) {
  description = "Check Kotlin code style."
  main = "com.pinterest.ktlint.Main"
  classpath = configurations.ktlint
  args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
  description = "Fix Kotlin code style deviations."
  main = "com.pinterest.ktlint.Main"
  classpath = configurations.ktlint
  args "-F", "src/**/*.kt"
}

configurations.all {
  resolutionStrategy {
    force "androidx.test:core:1.2.0"
    force "androidx.test:monitor:1.2.0"
  }
}