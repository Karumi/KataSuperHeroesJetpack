apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'shot'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.karumi"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.karumi.TestRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

configurations {
    ktlint
}

dependencies {
    /* Lint */
    ktlint 'com.github.shyiko:ktlint:0.29.0'

    /* Kotlin */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    /* Jetpack */
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.lifecycle:lifecycle-runtime:2.0.0"
    implementation "androidx.room:room-runtime:2.1.0-alpha04"
    kapt "androidx.room:room-compiler:2.1.0-alpha04"
    implementation "androidx.room:room-coroutines:2.1.0-alpha04"

    /* Coroutines */
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.0"

    /* DI */
    implementation 'org.kodein.di:kodein-di-erased-jvm:6.0.1'
    implementation 'org.kodein.di:kodein-di-framework-android-x:6.0.1'

    /* UI */
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "com.google.android.material:material:1.0.0"

    /* Testing */
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.23.4"
    androidTestImplementation "org.mockito:mockito-android:2.23.4"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    androidTestImplementation "com.github.tmurakami:dexopener:2.0.0"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:3.0.2") {
        exclude group: "com.android.support"
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"

    androidTestImplementation('com.facebook.testing.screenshot:core:0.8.0') {
        exclude group: 'com.crittercism.dexmaker', module: 'dexmaker'
        exclude group: 'com.crittercism.dexmaker', module: 'dexmaker-dx'
    }

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.1.0"
    testImplementation "androidx.room:room-testing:2.1.0-alpha04"
}

shot {
    appId = 'com.karumi'
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
}